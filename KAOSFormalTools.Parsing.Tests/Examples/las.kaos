begin goal
  id          achieve_ambulance_on_scene_when_incident_reported
  name        "Achieve [AmbulanceOnScene When IncidentReported]"
  definition  "Sooner-or-later, ambulance shall be on scene when incident is reported."
  formalspec  "G (incidentReported -> F ambulanceOnScene)"
  
  refinedby   achieve_ambulance_allocated_when_incident_reported, 
              achieve_ambulance_on_scene_when_ambulance_allocated
end

begin goal
  id          achieve_ambulance_allocated_when_incident_reported
  name        "Achieve [AmbulanceAllocated When IncidentReported]"
  definition  "Sooner-or-later, ambulance shall be allocated when incident is reported."
  formalspec  "G (incidentReported -> F ambulanceAllocated)"
end

begin goal
  id          achieve_ambulance_on_scene_when_ambulance_allocated
  name        "Achieve [AmbulanceOnScene When AmbulanceAllocated]"
  definition  "Sooner-or-later, ambulance shall be on scene when allocated."
  formalspec  "G (ambulanceAllocated -> F ambulanceOnScene)"
  
  refinedby   achieve_ambulance_mobilized_when_ambulance_allocated, 
              achieve_ambulance_on_scene_when_ambulance_mobilized
end

begin goal
  id          achieve_ambulance_mobilized_when_ambulance_allocated
  name        "Achieve [AmbulanceMobilized When AmbulanceAllocated]"
  definition  "Sooner-or-later, ambulance shall be mobilized when allocated."
  formalspec  "G (ambulanceAllocated -> F ambulanceMobilized)"
  
  refinedby   achieve_ambulance_mobilized_when_ambulance_allocated_on_road, 
              achieve_ambulance_mobilized_when_ambulance_allocated_at_station
end

begin goal
  id           achieve_ambulance_on_scene_when_ambulance_mobilized
  name         "Achieve [AmbulanceOnScene When AmbulanceMobilized]"
  definition   "Sooner-or-later, ambulance shall be on scene when mobilized."
  formalspec   "G (ambulanceMobilized -> F ambulanceOnScene)"
  
  obstructedby mobilized_ambulance_not_on_scene_in_time
end

begin goal
  id          achieve_ambulance_mobilized_when_ambulance_allocated_on_road
  name        "Achieve [AmbulanceMobilized When AmbulanceAllocatedOnRoad]"
  definition  "Sooner-or-later, ambulance shall be mobilized when allocated and on road."
  formalspec  "G (ambulanceAllocated & onRoad -> F ambulanceMobilized)"
end

begin goal
  id          achieve_ambulance_mobilized_when_ambulance_allocated_at_station
  name        "Achieve [AmbulanceMobilized When AmbulanceAllocatedAtStation]"
  definition  "Sooner-or-later, ambulance shall be mobilized when allocated and at station."
  formalspec  "G (ambulanceAllocated & !onRoad -> F ambulanceMobilized)"
  
  refinedby   achieve_ambulance_mobilized_by_fax, 
              achieve_ambulance_mobilized_by_phone
end

begin goal
  id          achieve_ambulance_mobilized_by_fax
  name        "Achieve [AmbulanceMobilizedByFax When AmbulanceAllocatedAtStation]"
  definition  "Sooner-or-later, ambulance shall be mobilized by fax when allocated and at station."
  formalspec  "G (ambulanceAllocated & !onRoad -> F ambulanceMobilizedByFax)"
end

begin goal
  id          achieve_ambulance_mobilized_by_phone
  name        "Achieve [AmbulanceMobilizedByPhone When AmbulanceAllocatedAtStation]"
  definition  "Sooner-or-later, ambulance shall be mobilized by phone when allocated and at station."
  formalspec  "G (ambulanceAllocated & !onRoad -> F ambulanceMobilizedByPhone)"
end

begin obstacle
  id          mobilized_ambulance_not_on_scene_in_time
  name        "Mobilized Ambulance Not On Scene in Time"
  formalspec  "F (ambulanceMobilized & G (!ambulanceOnScene))"
  
  refinedby   ambulance_stuck_in_traffic_jam
  refinedby   ambulance_lost
  refinedby   ambulance_broken_down
end

begin obstacle
  id          ambulance_stuck_in_traffic_jam
  name        "Ambulance Stuck In Traffic Jam"
  formalspec  "F (ambulanceMobilized & G (ambulanceInTrafficJam))"
end

begin obstacle
  id          ambulance_lost
  name        "Ambulance Lost"
  formalspec  "F (ambulanceMobilized & G (ambulanceLost))"
  refinedby   ambulance_not_in_familiar_area, gps_not_working
end

begin obstacle
  id          ambulance_broken_down
  name        "Ambulance Broken Down"
  formalspec  "F (ambulanceMobilized & G (ambulanceBrokenDown))"
end

begin obstacle
  id          ambulance_not_in_familiar_area
  name        "Ambulance Not In Familiar Area"
  formalspec  "F (ambulanceMobilized & G (!ambulanceInFamiliarArea))"
end

begin obstacle
  id          gps_not_working
  name        "GPS Not Working"
  formalspec  "F (ambulanceMobilized & G (gpsNotWorking))"
end

begin domainproperty
  id          ambulance_on_scene_not_in_traffic_jam
  name        "Ambulance On Scene Not In Traffic Jam"
  formalspec  "G (ambulanceOnScene -> !ambulanceInTrafficJam)"
end

begin domainproperty
  id          ambulance_on_scene_not_lost
  name        "Ambulance On Scene Not Lost"
  formalspec  "G (ambulanceOnScene -> !ambulanceLost)"
end

begin domainproperty
  id          ambulance_on_scene_not_broken_down
  name        "Ambulance On Scene Broken Down"
  formalspec  "G (ambulanceOnScene -> !ambulanceBrokenDown)"
end

begin domainproperty
  id          ambulance_lost_iff_gps_not_working_and_not_in_familiar_area
  name        "Ambulance Lost Iff GPS Not Working And Not In Familiar Area"
  formalspec  "G (ambulanceLost <-> (!ambulanceInFamiliarArea & gpsNotWorking))"
end

begin domainproperty
  id          by_fax_or_by_phone_mobilization
  name        "Ambulance Mobilized By Fax or Phone"
  formalspec  "G ((ambulanceMobilizedByFax | ambulanceMobilizedByPhone) -> ambulanceMobilized)"
end